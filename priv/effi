#!/usr/bin/env escript
%% -*- erlang -*-
%
% Cuneiform: A Functional Language for Large Scale Scientific Data Analysis
%
% Copyright 2016 JÃ¶rgen Brandt, Marc Bux, and Ulf Leser
%
% Licensed under the Apache License, Version 2.0 (the "License");
% you may not use this file except in compliance with the License.
% You may obtain a copy of the License at
%
%    http://www.apache.org/licenses/LICENSE-2.0
%
% Unless required by applicable law or agreed to in writing, software
% distributed under the License is distributed on an "AS IS" BASIS,
% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
% See the License for the specific language governing permissions and
% limitations under the License.

-author( "Jorgen Brandt <brandjoe@hu-berlin.de>" ).

-define( VSN, "0.1.0" ).
-define( BUILD, "2016-03-11" ).

%% main/1
%
main( [] ) ->
  print_intro(),
  print_usage();

main( CmdLine ) ->

  case getopt:parse( opt_spec_list(), CmdLine ) of
    {ok, {OptList, NonOptList}} ->
      case lists:member( version, OptList ) of
        true  -> print_vsn();
        false ->
          case lists:member( help, OptList ) of
            true  ->
              print_intro(),
              print_usage();
            false ->
              case lists:member( cite, OptList ) of
                true  -> print_bibtex_entry();
                false ->
                  [{dir, Dir}] = OptList,
                  runscript( Dir, NonOptList )
              end
          end
      end;
    {error, {Reason, Data}} ->
      error( {Reason, Data} )
  end.

%% print_usage/0
%
print_usage() -> getopt:usage( opt_spec_list(), "effi", "<requestfile> <summaryfile>" ).

%% opt_spec_list/0
%
opt_spec_list() ->
  [
   {version,  $v, "version",  undefined,         "Show Effi version"},
   {help,     $h, "help",     undefined,         "Show command line options"},
   {cite,     $c, "cite",     undefined,         "Show Bibtex entry for citation"},
   {dir,      $d, "dir",      {string, "."},     "Working directory"}
  ].

%% print_bibtex_entry/0
%
print_bibtex_entry() -> io:format( "~n~s~n~n", [bibtex_entry()] ).

%% bibtex_entry/0
%
bibtex_entry() ->
  string:join( ["@InProceedings{Brandt2015,",
         "  Title                    = {Cuneiform: A Functional Language for Large Scale Scientific Data Analysis},",
         "  Author                   = {Brandt, J{\"o}rgen and Bux, Marc and Leser, Ulf},",
         "  Booktitle                = {Proceedings of the Workshops of the EDBT/ICDT},",
         "  Year                     = {2015},",
         "  Address                  = {Brussels, Belgium},",
         "  Month                    = {March},",
         "  Pages                    = {17--26},",
         "  Volume                   = {1330},",
         "  Abstract                 = {The need to analyze massive scientific data sets on the one hand and the availability of distributed compute resources with an increasing number of CPU cores on the other hand have promoted the development of a variety of languages and systems for parallel, distributed data analysis. Among them are data-parallel query languages such as Pig Latin or Spark as well as scientific workflow languages such as Swift or Pegasus DAX. While data-parallel query languages focus on the exploitation of data parallelism, scientific workflow languages focus on the integration of external tools and libraries. However, a language that combines easy integration of arbitrary tools, treated as black boxes, with the ability to fully exploit data parallelism does not exist yet. Here, we present Cuneiform, a novel language for large-scale scientific data analysis. We highlight its functionality with respect to a set of desirable features for such languages, introduce its syntax and semantics by example, and show its flexibility and conciseness with use cases, including a complex real-life workflow from the area of genome research. Cuneiform scripts are executed dynamically on the workflow execution platform Hi-WAY which is based on Hadoop YARN. The language Cuneiform, including tool support for programming, workflow visualization, debugging, logging, and provenance-tracing, and the parallel execution engine Hi-WAY are fully implemented.},",
         "  Doi                      = {10.13140/RG.2.1.3547.6561},",
         "  Url                      = {http://ceur-ws.org/Vol-1330/paper-03.pdf}",
         "}"], "\n" ).

print_intro() ->
  io:format( "~s~n~n", [intro()] ).

intro() ->
  string:join( [
      "   ._,,,,  ,,_,=_",
      "    W   `_@__#__     The Erlang Foreign Function Interface (Effi) allows the",
      "   @P+#   F @F @     execution of functions defined in different programming",
      "  _W   y @  # qF     languages (e.g., Bash, Python, or R) by specifying the",
      "  ^^^^^  P qF  `     function's arguments, body and output values.",
      "",
      "Copyright 2016 Jorgen Brandt <brandjoe@hu-berlin.de>"

    ], "\n" ).

%% print_vsn/0
%
print_vsn() -> io:format( "Effi version ~s build ~s~n", [?VSN, ?BUILD] ).

%% runscript/2
%
runscript( Dir, [RequestFile, SumFile] ) ->

  % read script from file
  B = case file:read_file( RequestFile ) of
        {error, R1} -> error( {R1, RequestFile} );
        {ok, X}     -> X
      end,

  % parse line
  {ok, Tokens, _} = erl_scan:string( binary_to_list( B ) ),
  {Lam, Fa} = case erl_parse:parse_term( Tokens ) of
                {error, Reason} -> error( Reason );
                {ok, Y}         -> Y
              end, 

  % run script
  Summary = case effi:check_run( Lam, Fa, Dir ) of

              {failed, R2, MissingList} ->
                error( {R2, MissingList} );

              {failed, script_error, {ActScript, Out}}     ->

                % print tool output
                ok = lists:foreach( fun( Line ) ->
                	              io:format( "~s~n", [Line] )
                	            end,
                	            Out ),

                % print actual script
                ok = io:format( "[script]~n" ),
                _ = lists:foldl( fun( Line, N ) ->
      	                           ok = io:format( "~4.B  ~s~n", [N, Line] ),
          	                   N+1
          	                 end,
          	                 1, string:tokens( ActScript, "\n" ) ),

                % throw error
                error( script_error );

              {finished, Sum} -> Sum
            end,


  % write summary
  case file:write_file( SumFile, io_lib:format( "~p~n", [Summary] ) ) of
    {error, R4} -> error( {R4, SumFile} );
    ok          -> ok
  end;



runscript( _Dir, NonOptList ) ->
  error( {request_and_summary_expected, NonOptList} ).


